<program> ::= <fdecls> <declarations> <statement_seq> .
<fdecls> ::= <fdec> ; <*fdecls>
<*fdecls> ::= <fdec> ; <*fdecls>
<*fdecls> ::= 𝛜
<fdec> ::= def <type> <var> ( <params> ) <declarations> <statement_seq> fed
<params> ::= <type> <var> <*params>
<params> ::= 𝛜
<*params> ::= , <type> <var> <*params>
<*params> ::= 𝛜
<declarations> ::= <decl> ; <*declarations>
<declarations> ::= 𝛜
<*declarations> ::= <decl> ; <*declarations>
<*declarations> ::= 𝛜
<decl> ::= <type> <varlist>
<type> ::= int
<type> ::= double
<varlist> ::= <var> <*varlist>
<*varlist> ::= , <var> <*varlist>
<*varlist> ::= 𝛜
<statement_seq> ::= <statement> <*statement_seq>
<*statement_seq> ::= ; <statement> <*statement_seq>
<*statement_seq> ::= 𝛜
<statement> ::= <var> = <expr>
<statement> ::= if <bexpr> then <statement_seq> <ifstatement> fi
<statement> ::= while <bexpr> do <statement_seq> od
<statement> ::= <command> <expr>
<statement> ::= 𝛜
<ifstatement> ::= else <statement_seq>
<ifstatement> ::= 𝛜
<command> ::= print
<command> ::= return
<expr> ::= <term> <*expr>
<*expr> ::= <sign> <term> <*expr>
<*expr> ::= 𝛜
<sign> ::= +
<sign> ::= -
<term> ::= <factor> <*term>
<*term> ::= <operation> <factor> <*term>
<*term> ::= 𝛜
<operation> ::= *
<operation> ::= /
<operation> ::= %
<factor> ::= <var> <*factor>
<factor> ::= <integer>
<factor> ::= ( <expr> )
<*factor> ::= ( <exprseq> )
<*factor> ::= 𝛜
<exprseq> ::= <expr> <*exprseq>
<exprseq> ::= 𝛜
<*exprseq> ::= , <expr> <*exprseq>
<*exprseq> ::= 𝛜
<bexpr> ::= <bterm> <*bexpr>
<*bexpr> ::= or <bterm> <*bexpr>
<*bexpr> ::= 𝛜
<bterm> ::= <bfactor> <*bterm>
<*bterm> ::= and <bfactor> <*bterm>
<*bterm> ::= 𝛜
<bfactor> ::= ( <bexpr> )
<bfactor> ::= not <bfactor>
<bfactor> ::= ( <expr> <comp> <expr> )
<comp> ::= <
<comp> ::= >
<comp> ::= ==
<comp> ::= <=
<comp> ::= >=
<comp> ::= <>
<var> ::= <id> <array>
<array> ::= [ <expr> ]
<array> ::= 𝛜
<id> ::= <letter> <*id>
<*id> ::= <symbol> <*id>
<*id> ::= 𝛜
<symbol> ::= <letter>
<symbol> ::= <digit>
<integer> ::= <digit> <*integer>
<*integer> ::= <integer>
<*integer> ::= . <double>
<*integer> ::= 𝛜
<double> ::= <integer> <double>
<double> ::= 𝛜
<letter> ::= a
<letter> ::= b
<letter> ::= c
<letter> ::= d
<letter> ::= e
<letter> ::= f
<letter> ::= g
<letter> ::= h
<letter> ::= i
<letter> ::= j
<letter> ::= k
<letter> ::= l
<letter> ::= m
<letter> ::= n
<letter> ::= o
<letter> ::= p
<letter> ::= q
<letter> ::= r
<letter> ::= s
<letter> ::= t
<letter> ::= u
<letter> ::= v
<letter> ::= w
<letter> ::= x
<letter> ::= y
<letter> ::= z
<digit> ::= 0
<digit> ::= 1
<digit> ::= 2
<digit> ::= 3
<digit> ::= 4
<digit> ::= 5
<digit> ::= 6
<digit> ::= 7
<digit> ::= 8
<digit> ::= 9